apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

ext {
    PUBLISH_GROUP_ID = 'io.github.sunshaobei'
    PUBLISH_VERSION = '1.0.0'
    PUBLISH_ARTIFACT_ID = "sliver"
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

afterEvaluate {

    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up in a moment
                from components.release
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` and the sources
//            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

//                artifact './build/outputs/aar/sliver-release.aar'
                artifact androidSourcesJar
                // Self-explanatory metadata for the most part
                pom {
                    name = 'satis-sliver'
                    description = 'satis-sliver'
                    url = 'https://github.com/sunshaobei/Satis'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                            comments = 'A business-friendly OSS license'
                        }
                    }

                    developers {
                        developer {
                            id = 'sunshaobei'
                            name = 'sunshaobei'
                            email = 'sunshaobei@foxmail.com'
                            url = 'https://github.com/sunshaobei/Satis'
                        }
                    }
                    scm {
                        url = 'https://github.com/sunshaobei/Satis'
                        connection = 'scm:git@https://github.com/sunshaobei/Satis.git'
                        developerConnection = 'scm:git@https://github.com/sunshaobei/Satis.git'
                    }
                }
            }
        }
        repositories {
            // The repository to publish to, Sonatype/MavenCentral
            maven {
                // This is an arbitrary name, you may also use "mavencentral" or
                // any other name that's descriptive for you
                name = "mavencentral"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                // You only need this if you want to publish snapshots, otherwise just set the URL
                // to the release repo directly
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                // The username and password we've fetched earlier
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}
signing {
    sign publishing.publications
}


// 58584D12
