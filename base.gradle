def base_plugins(Project project, boolean isModule){
    if (isModule){
        project.apply plugin: 'com.android.application'
    }else {
        project.apply plugin: 'com.android.library'
    }
    project.apply plugin: 'kotlin-android'
    project.apply plugin: 'kotlin-kapt'
}
ext.base_plugins = this.&base_plugins

def base_android(Project project,boolean isModule){
    project.android {
        compileSdkVersion build_versions.compile_sdk
        defaultConfig {
            minSdkVersion build_versions.min_sdk
            targetSdkVersion build_versions.target_sdk
            if (isModule) {
                moduleName = project.getName().replace("-","_")
                applicationId "cn.paperang.$moduleName"
            }
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            consumerProguardFiles "consumer-rules.pro"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        dataBinding{
            enabled = true
        }
        kotlinOptions {
            jvmTarget = '1.8'
        }

        sourceSets {
            main {
                if (isModule) {
                    manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                }
            }
        }
    }
}
ext.base_android = this.&base_android